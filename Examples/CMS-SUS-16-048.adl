#info analysis
#  experiment CMS
#  id SUS-16-048
#  publication Phys. Lett. B 782 (2018) 440
#  sqrtS 13.0
#  lumi 35.9
#  arXiv 1801.01846
#  hepdata 
#  doi 10.1016/j.physletb.2018.05.062

object muons : MUO
  select {MUO_}Pt [] 3.5 30
  select abs({MUO_}Eta) < 2.4
# e.g. select IsolationVarRhoCorr < 0.1
# Contained in the provided CMS efficiencies

object electrons : ELE
  select {ELE_}Pt [] 3.5 30
  select Abs({ELE_}Eta) < 2.5
# select some ID and iso - check with Delphes
# Contained in the provided CMS efficiencies
# select pT < 10 ? loose == 1 : tight == 1
  
object jets : JET
  select {JET_}Pt > 25  
  select Abs({JET_}Eta) < 2.4

object bjets : jets
  select {JET_}BTag == 1

define dimuon = muons[0] muons[1]
define MTm1 = sqrt( 2*{muons[0]}Pt * MET*(1-cos( {METLV[0]}Phi - {muons[0]}Phi )))
define MTm2 = sqrt( 2*{muons[1]}Pt * MET*(1-cos( {METLV[0]}Phi - {muons[1]}Phi )))

region w1dimuon
  select ALL
  weight xsec 0.688016
  #weight xsec 0.6895
  select Size(muons) == 2
  select {muons[0]}Pt [] 5 30
  select {muons[1]}Pt [] 5 30
  select {muons[0]}q * {muons[1]}q == -1
  select {dimuon}Pt > 3 
  select {dimuon}m [] 4 50 
  select {dimuon}m ][ 9 10.5
  select {METLV[0]}Pt [] 125 200
  weight trigger 0.65 
  select Size(jets) >= 1
  select HT(jets) > 100
  select {METLV[0]}Pt / HT(jets) [] 0.6 1.4
  select Size(bjets) == 0
  reject fMTauTau(muons[0], muons[1, METLV[0]) [] 0 160 
  select MTm1 < 70 AND MTm2 < 70

