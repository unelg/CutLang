# ADL/CL example 11: Printing and saving output 
#   - "print" keyword is used for printing quantities during run time.
#     Particularly useful for debugging.
#   - "save" keyword is used for saving quantities in CSV (and soon ROOT ntuple) formats.
#     e.g. to provide input to a machine learning training.
# 
# IMPORTANT: Note that printing and saving are not a part of fundamental analysis algorithm 
# description. "print" and "save" keywords are auxiliaries used in analysis execution.

info analysis
  title "ADL/CL ex11: Printing and saving output"

# OBJECT DEFINITIONS
object jets
  take Jet
  select pT(Jet) > 50
  select abs(eta(Jet)) < 2.4

object leptons : Union(ELE, MUO)

define MTl = sqrt( 2*pT(leptons[0]) * MET*(1-cos(Phi(METLV[0]) - Phi(leptons[0]) )))
define HT = fHT(jets)
define MT2j0j1 = fMT2(Jet[0], Jet[1], METLV[0])
define HTlep = fHT(jets) + pT(leptons[0])

# EVENT SELECTION
region hadronic
 select size(jets) >= 4 
 select size(leptons) == 0
 select HT > 300 
 select dphi(jets[0], METLV[0]) > 0.5
 select dphi(jets[1], METLV[0]) > 0.5
 select dphi(jets[2], METLV[0]) > 0.5
 select dR(jets[0], jets[1]) > 1.0
 select MT2j0j1 > 300 
 print size(jets) pT(jets[0]) HT dphi(jets[0], METLV[0]) MT2j0j1 MET
 save hadvars csv size(jets) pT(jets[0]) HT dphi(jets[0], METLV[0]) MT2j0j1 MET

region leptonic
  select size(jets) >= 3  
  select size(leptons) == 1
  select HTlep > 300
  select MTl > 150
  print pT(leptons[0]) size(jets) HTlep MTl MET
  save lepvars csv pT(leptons[0]) size(Jet) HTlep MTl MET


